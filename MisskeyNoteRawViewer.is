/// @ 0.17.0
### {
  name: "ノートのRawデータを表示する"
  version: "0.1.0"
  author: "はとぽぷ(@hatopop_vr@misskey.io)"
  description: "ノートのRawデータを表示するプラグインです。"
  permissions: ["read:reactions"]
  config: {
    indent: {
      type: "number",
      label: "インデント",
      description: "JSONのインデントを設定します。",
      default: 2
    }
  }
}

// ServerNameの取得
let SERVER_NAME = Mk:url().split('/')[2]
// インデントの取得
let INDENT = Plugin:config.indent
var INDENT_STR = ""
for INDENT INDENT_STR = `{INDENT_STR} `

// 日時をフォーマットして取得する関数
@getCurrentDateTime() {
    return `{Date:year()}-{Date:month()}-{Date:day()} {Date:hour()}:{Date:minute()}:{Date:second()}.{Math:floor(Date:now()%1000/100)}`
}

// ログ出力関数を定義
@logInfo(message) {
    let date_time = getCurrentDateTime()
    print(`{date_time} - INFO - {message}`)
}

@logError(message) {
    let date_time = getCurrentDateTime()
    print(`{date_time} - ERROR - {message}`)
}

// オブジェクトを整形されたJSON形式にフォーマットする関数
@obj2json(obj){
  let funcObj = @(obj, nest){
    var json = ""
    let next_nest = `{nest}{INDENT_STR}`
    if (Core:type(obj) == "obj") {
      let keys = Obj:keys(obj)
      if (keys.len == 0) {
        return `{json}\{\}`
      }
      json = `{json}\{`
      each let key keys json = `{json}{Str:lf}{next_nest}"{key}": {funcObj(Obj:get(obj key), next_nest)},`
      // 最後のカンマを削除
      json = json.slice(0, json.len - 1)
      json = `{json}{Str:lf}{nest}\}`
      return json
    }
    if (Core:type(obj) == "arr") {
      if (obj.len == 0) {
        return `{json}[]`
      }
      json = `{json}[`
      each let item obj json = `{json}{Str:lf}{next_nest}{funcObj(item, next_nest)},`
      // 最後のカンマを削除
      json = json.slice(0, json.len - 1)
      json = `{json}{Str:lf}{nest}]`
      return json
    }
    if (Core:type(obj) == "str") {
      return `"{obj}"`
    }
    return `{obj}`
  }
  return funcObj(obj, "")
}

@misskeyNoteRawViewer(note) {
    logInfo(`==================================================`)
    logInfo(`Viewing raw for note ID {note.id}`)

    logInfo(`Displaying raw for note ID {note.id} in formatted JSON format`)
    var formatted_note_raw_json = obj2json(note)
    var result_json = `\`\`\`json
{formatted_note_raw_json}
\`\`\``
    Mk:dialog(`**Raw: JSON formatted**` result_json)

    logInfo(`Completed viewing raw for note ID {note.id}`)
    logInfo(`==================================================`)
}

// ノートのRawデータを表示するアクションを登録
Plugin:register_note_action(`</>Raw` @(note) {misskeyNoteRawViewer(note)})